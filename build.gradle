plugins {
	id 'org.springframework.boot' version '2.6.9'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.openapi.generator' version '4.2.2'
	id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.5'
	id 'java'
}

idea.project.settings {
	taskTriggers {
		beforeBuild tasks.getByName("openApiGenerate")
		beforeRebuild tasks.getByName("openApiGenerate")
		afterSync tasks.getByName("openApiGenerate")
		beforeSync tasks.getByName("openApiGenerate")
	}
}

group = 'com.testproj'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '18'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'io.springfox:springfox-swagger2:2.9.2'
	implementation 'org.springdoc:springdoc-openapi-ui:1.2.21'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "${projectDir}/src/main/resources/api/contacts-spec.yaml"
	outputDir = "${buildDir}/generated/open-api"
	apiPackage = "com.testproj.contacts.api"
	invokerPackage = "com.testproj.contacts.handler"
	modelPackage = "com.testproj.contacts.model"
	modelNameSuffix = "Json"
	configOptions = [
			delegatePattern: "true"
	]
}

compileJava {
	dependsOn  tasks.openApiGenerate
	source "${buildDir}/generated/open-api/src/main/java"
}

springBoot {
	mainClass = "com.testproj.contacts.ContactsApplication"
}
sourceSets {
	main {
		java {
			srcDir "${buildDir.absolutePath}/generated/open-api/src/main/java"
		}
	}
}
tasks.named('test') {
	useJUnitPlatform()
}
